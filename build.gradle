buildscript {
    ext {
        contractVersion = '1.0.0'
        protobufVersion = '3.19.1'
        protobufPluginVersion = '0.8.18'
        grpcVersion = '1.42.2'
        jakartaVersion = '1.3.5' // Java 9+ compatibility - Do NOT update to 2.0.0
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.google.protobuf' version "${protobufPluginVersion}"
}

group 'org.akshit'
version "${contractVersion}-SNAPSHOT"

java{
    toolchain{
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    compileOnly "jakarta.annotation:jakarta.annotation-api:${jakartaVersion}"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.akshit'
            artifactId = 'grpc-interface'
            version = "${contractVersion}"
            from components.java
        }
    }
}